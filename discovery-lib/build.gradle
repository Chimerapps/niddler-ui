plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply from: '../dependencies.gradle'

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: "com.github.ben-manes.versions"
apply plugin: 'maven-publish'

def artifactVersion = versions.discoveryLib
def artifactGroup = 'com.chimerapps'
def artifactName = "discovery-lib"

dependencies {
    deps.discoveryCompile.each { name, dep ->
        api dep
    }

    deps.discoveryTestCompile.each { name, dep ->
        testImplementation dep
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = "$artifactName"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Chimerapps/niddler-ui'

        version {
            name = "$artifactVersion"
            desc = "Base library for discovering devices/sessions for niddler, PreferenceSpy, ..."
            released = new Date()
        }
    }
    publications = ['maven']
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Nicola Verbeeck"
            email "nicola@chimerapps.com"
            organization "Chimerapps"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "$artifactGroup"
            artifactId "$artifactName"
            version "$artifactVersion"

            from components.java

            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Base library for discovering devices/sessions for niddler, PreferenceSpy, ...')
                root.appendNode('name', "$artifactName")
                root.appendNode('url', 'https://github.com/Chimerapps/niddler-ui')
                root.children().last() + pomConfig
            }
        }
    }
}