plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

apply from: '../dependencies.gradle'

apply plugin: "org.jetbrains.intellij"
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

def artifactVersion = versions.discoveryPluginLib
def artifactGroup = 'com.chimerapps'
def artifactName = "discovery-plugin-ui"

group "$artifactGroup"
version "$artifactVersion"

sourceCompatibility = 1.8

intellij {
    version versions.minIntelliJ
    updateSinceUntilBuild false
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    deps.discoveryPluginCompile.each { name, dep ->
        api dep
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = "$artifactName"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Chimerapps/niddler-ui'

        version {
            desc = "Plugin UI base library for discovering devices/sessions for niddler, PreferenceSpy, ..."
            name = "$artifactVersion"
            released = new Date()
        }
    }
    publications = ['maven']
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Nicola Verbeeck"
            email "nicola@chimerapps.com"
            organization "Chimerapps"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "$artifactGroup"
            artifactId "$artifactName"
            version "$artifactVersion"

            from components.java

            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Plugin UI base library for discovering devices/sessions for niddler, PreferenceSpy, ...')
                root.appendNode('name', "$artifactName")
                root.appendNode('url', 'https://github.com/Chimerapps/niddler-ui')
                root.children().last() + pomConfig
            }
        }
    }
}